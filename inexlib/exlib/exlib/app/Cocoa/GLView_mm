// Copyright (C) 2010, Guy Barrand. All rights reserved.
// See the file exlib.license for terms.

#import "GLView.h"

@implementation GLView

- (id)initWithFrame:(NSRect)a_rect {

  NSOpenGLPixelFormatAttribute att[32];
  int i = 0;
  att[i++] = NSOpenGLPFADoubleBuffer;
  att[i++] = NSOpenGLPFAAccelerated;
  att[i++] = NSOpenGLPFAAccumSize;
  att[i++] = (NSOpenGLPixelFormatAttribute)32;
  att[i++] = NSOpenGLPFAColorSize;
  att[i++] = (NSOpenGLPixelFormatAttribute)32;
  att[i++] = NSOpenGLPFADepthSize;
  att[i++] = (NSOpenGLPixelFormatAttribute)32;

  // Antialiasing :
  att[i++] = NSOpenGLPFASampleBuffers;
  att[i++] = 1;
  att[i++] = NSOpenGLPFASamples;
  att[i++] = 2;
  att[i++] = NSOpenGLPFANoRecovery;

  att[i] = (NSOpenGLPixelFormatAttribute)0;
  NSOpenGLPixelFormat *pixelFormat = 
    [[NSOpenGLPixelFormat alloc] initWithAttributes:att];

  if (self = [super initWithFrame:a_rect pixelFormat:pixelFormat]) {
    // flush buffer only during the vertical retrace of the monitor
    const GLint vals[1] = {1};
    [[self openGLContext] setValues:vals forParameter:NSOpenGLCPSwapInterval];
    m_main = 0;
  }

  [pixelFormat release];
  return self;
}

- (void)dealloc {  
  [super dealloc];
}

- (void)set_main:(app_main_t*)a_main{m_main = a_main;}

/////////////////////////////////////////////////////////////////////
/////////////////////////////////////////////////////////////////////
/////////////////////////////////////////////////////////////////////

- (void)drawRect:(NSRect)rect {
  [[self openGLContext] makeCurrentContext];
  int w = rect.size.width;
  int h = rect.size.height;

  if(m_main) {
    m_main->set_size(w,h);
    m_main->render();
  }

  [[self openGLContext] flushBuffer];
}
- (void)mouseDown:(NSEvent*)a_event {  
  NSPoint p = [a_event locationInWindow];
  //(0,0) = bottom left of window.
  //NSLog(@"debug : GLView::mouseDown %g %g",p.x,p.y);
  if(m_main) {
    if(m_main->touch_down(p.x,p.y)) m_main->win_render();
    //if(m_main->num_cbks()) {
      //[m_app_delegate start_work_timer];
    //}
  }
}

- (void)mouseUp:(NSEvent*)a_event {  
  NSPoint p = [a_event locationInWindow];
  //(0,0) = bottom left of window.
  //NSLog(@"debug : GLView::mouseUp %g %g",p.x,p.y);
  if(m_main) {
    m_main->add_work_check_arm_buttons();
    if(m_main->touch_up(p.x,p.y)) m_main->win_render();
    //if(m_main->num_cbks()) {
      //[m_app_delegate start_work_timer];
    //}
  }
}

- (void)mouseDragged:(NSEvent*)a_event {  
  NSPoint p = [a_event locationInWindow];
  //(0,0) = bottom left of window.
  //NSLog(@"debug : GLView::mouseDragged %g %g",p.x,p.y);
  if(m_main) {
    if(m_main->touch_move(p.x,p.y)) m_main->win_render();
  }
}

- (void)mouseMoved:(NSEvent*)a_event {  
  NSPoint p = [a_event locationInWindow];
  //(0,0) = bottom left of window.
  //NSLog(@"debug : GLView::mouseMoved %g %g",p.x,p.y);
  if(m_main) {
    if(m_main->mouse_motion(p.x,p.y)) m_main->win_render();
  }
}

- (BOOL)acceptsFirstResponder {return YES;} //so that keyDown be called.

- (void)keyDown:(NSEvent*)a_event {  
  //NSLog(@"debug : GLView::keyDown");

  if(!m_main) return;

  bool shift = [a_event modifierFlags] & NSShiftKeyMask;
  bool ctrl = [a_event modifierFlags] & NSControlKeyMask;

  //NSString* chars = [a_event charactersIgnoringModifiers];
  NSString* chars = [a_event characters];
  unichar keyChar = 0;
  if ( [chars length] == 0 ) return;
  if ( [chars length] != 1 ) {[super keyDown:a_event];return;}

  keyChar = [chars characterAtIndex:0];

  if ( keyChar == NSLeftArrowFunctionKey ) {
    if(m_main->do_key(inlib::sg::key_left,shift,ctrl))
      m_main->win_render();
  } else if ( keyChar == NSRightArrowFunctionKey ) {
    if(m_main->do_key(inlib::sg::key_right,shift,ctrl))
      m_main->win_render();
  } else if ( keyChar == NSUpArrowFunctionKey ) {
    if(m_main->do_key(inlib::sg::key_up,shift,ctrl))
      m_main->win_render();
  } else if ( keyChar == NSDownArrowFunctionKey ) {
    if(m_main->do_key(inlib::sg::key_down,shift,ctrl))
      m_main->win_render();

  } else if ( keyChar == NSTabCharacter ) {
    if(m_main->do_tab(false)) m_main->win_render();
  } else if ( keyChar == 25 ) { //shift+tab ! //ASCII 25=EM (end of medium)
    if(m_main->do_tab(true)) m_main->win_render();
  } else if ( keyChar == NSCarriageReturnCharacter ) {
    m_main->do_return();
    //if(m_main->num_cbks()) [m_app_delegate start_work_timer];
  } else if ( keyChar == 27 ) { //escape
    m_main->do_escape();
    m_main->win_render();
  }
  //NSLog(@"debug : GLView::keyDown : ??? %d \"%c\"",keyChar,keyChar);
}

@end
